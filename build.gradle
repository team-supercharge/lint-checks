import io.gitlab.arturbosch.detekt.Detekt

buildscript {
    ext.versions = [
            kotlin             : '1.7.20',
            minSdk             : 23,
            compileSdk         : 33,
            androidTools       : '30.3.1',
            detekt             : '1.22.0',
            androidGradlePlugin: '7.3.1',
            publishPlugin      : '0.23.1',
            junit              : '4.13.2'
    ]

    ext.deps = [
            'kotlin': [
                    'stdlib' : "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
                    'reflect': "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin}",
            ],
            'lint'  : [
                    'core'  : "com.android.tools.lint:lint:${versions.androidTools}",
                    'api'   : "com.android.tools.lint:lint-api:${versions.androidTools}",
                    'checks': "com.android.tools.lint:lint-checks:${versions.androidTools}",
                    'tests' : "com.android.tools.lint:lint-tests:${versions.androidTools}",
            ],
    ]

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.androidGradlePlugin}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"

        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${versions.detekt}"

        classpath "com.vanniktech:gradle-maven-publish-plugin:${versions.publishPlugin}"
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.22.0")
}

def projectSource = file(projectDir)
def configFile = files("$rootDir/config/detekt/detekt.yml")
def kotlinFiles = "**/*.kt"
def resourceFiles = "**/resources/**"
def buildFiles = "**/build/**"

tasks.register("detektAll", Detekt) {
    def autoFix = project.hasProperty('detektAutoFix')

    description = "Custom DETEKT build for all modules"
    parallel = true
    ignoreFailures = false
    autoCorrect = autoFix
    buildUponDefaultConfig = true
    setSource(projectSource)
    config.setFrom(configFile)
    include(kotlinFiles)
    exclude(resourceFiles, buildFiles)
    reports {
        html.enabled = true
        xml.enabled = false
    }
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:${versions.detekt}"
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
